{$CLEO .csi}

thread 'CARSPWN'
var 
    0@ : Integer
    1@ : Float
    2@ : Float
    3@ : Float
    4@ : Float
    5@ : Integer  // vehicle model id
    6@ : Float
    7@ : Float
    10@ : Integer // selected item index
    12@ : Integer // active item index
    14@ : Integer
    15@ : Integer
end

0DDD: 0@ = get_mutex_var 0
if or
    0@ == 1  
    8256: not player $303 defined        
then
    end_thread
end

0DDC: set_mutex_var 0 to 1
01B4: set_player $303 frozen_state 0

0DD0: 0@ = get_label_addr @menu_desc
0DF2: create_menu 0@ items 100
0DF5: set_menu_active_item_index 12@

// touch handle loop
while true
    wait 0
    // resets menu touch item index as well
    0DF4: 10@ = get_menu_touched_item_index maxtime 100
    // activate cheat
    if and
        10@ >= 0
        10@ < 100
    then
        5@ = 130
        while 10@ > 0
            10@ -= 1
            5@ += 1
            while true
                if or
                    5@ == 165 
                    5@ == 180 
                    5@ == 181 
                    5@ == 195 
                    5@ == 171
                    5@ == 194
                    5@ == 231 
                then
                    5@ += 1
                else
                    break
                end
            end
        end
        0247: request_model 5@
        038B: load_requested_models 
        while 8248: not model 5@ available
            wait 0
        end
        0054: store_player $303 position_to 1@ 2@ 3@
        0170: 4@ = player $303 z_angle
        
        6@ = 4@
        if 
            6@ < 270.0 
        then
            6@ += 90.0
        else
            6@ -= 270.0
        end
        
        02F7: 7@ = sin 6@
        7@ *= 7.0
        1@ += 7@
        
        02F6: 7@ = cos 6@
        7@ *= 7.0
        2@ += 7@
        
        if 
            4@ > 180.0
        then 
            4@ -= 180.0 
        else 
            4@ += 180.0
        end 
        
        00A5: 0@ = create_car 5@ at 1@ 2@ 3@
        4@ -= 90.0
        0175: set_car 0@ z_angle_to 4@
        020A: set_car 0@ door_status_to 0 
        0249: release_model 5@
        01C3: remove_references_to_car 0@ 
        
        break
    end
    // close menu
    if 
        10@ == -2
    then
        break
    end
end

0DF6: 12@ = get_menu_active_item_index

// wait to show what was selected and not to trigger player controls with current touch
wait 100           
         
0DF3: delete_menu
01B4: set_player $303 frozen_state 1
0DDC: set_mutex_var 0 to 0      

end_thread

:menu_desc
hex
    // flags
    01 00 00 00
    // title
    "CAR" 20 "SPAWNER" 00
    // close button
    "CLOSE" 00
    // items
    "LANDSTK" 00
    "IDAHO" 00
    "STINGER" 00
    "LINERUN" 00
    "PEREN" 00
    "SENTINL" 00
    "RIO" 00
    "FIRETRK" 00
    "TRASHM" 00
    "STRETCH" 00
    "MANANA" 00
    "INFERNS" 00
    "VOODOO" 00
    "PONY" 00	
    "MULE" 00	
    "CHEETAH" 00
    "AMBULAN" 00
    "FBICAR" 00
    "MOONBM" 00
    "ESPERAN" 00
    "TAXI" 00	
    "WASHING" 00
    "BOBCAT" 00
    "WHOOPEE" 00
    "BFINJC" 00
    "HUNTER" 00
    "POLICAR" 00
    "ENFORCR" 00
    "SECURI" 00
    "BANSHEE" 00
    "PREDATR" 00
    "BUS" 00			
    "RHINO" 00	
    "BARRCKS" 00
    "CUBAN" 00	
    "ANGEL" 00	
    "COACH" 00	
    "CABBIE" 00	
    "STALION" 00	
    "RUMPO" 00
    "ROMERO" 00
    "PACKER" 00
    "SENTXS" 00
    "ADMIRAL" 00
    "SQUALO" 00
    "SEASPAR" 00
    "PIZZABO" 00
    "GANGBUR" 00  
    "SPEEDER" 00	
    "REEFER" 00
    "TROPIC" 00
    "FLATBED" 00
    "YANKEE" 00
    "CADDY" 00	
    "ZEBRA" 00	
    "TOPFUN" 00
    "SKIMMER" 00
    "PCJ600" 00
    "FAGGIO" 00
    "FREEWAY" 00 
    "GLENDAL" 00
    "OCEANIC" 00
    "SANCHEZ" 00
    "SPARROW" 00
    "PATRIOT" 00
    "LOVEFIS" 00
    "COASTG" 00	
    "DINGHY" 00
    "HERMES" 00
    "SABRE" 00	
    "SABRETU" 00
    "PHOENIX" 00
    "WALTON" 00
    "REGINA" 00
    "COMET" 00
    "DELUXO" 00
    "BURRITO" 00
    "SPAND" 00
    "MARQUIS" 00
    "BAGGAGE" 00
    "KAUFMAN" 00
    "MAVERIC" 00
    "VCNMAV" 00
    "RANCHER" 00
    "FBIRANC" 00
    "VIRGO" 00
    "GREENWO" 00
    "CUBJET" 00	
    "HOTRING" 00
    "SANDKIN" 00
    "BLISTAC" 00
    "POLMAV" 00
    "BOXVILL" 00
    "BENSON" 00
    "MESA" 00   
    "HOTRINA" 00
    "HOTRINB" 00
    "BLOODRA" 00
    "BLOODRB" 00
    "CHEETAH" 00
    00
end