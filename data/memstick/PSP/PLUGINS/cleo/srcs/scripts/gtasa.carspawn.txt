{$CLEO .csi}

/*
    Car spawner for GTA San Andreas    
    Controls (touch points are explained in CLEO Android readme):
      activation - via cleo script menu
      prev vehicle - touch point 2
      next vehicle - touch point 8
      select - touch point 5
      exit - menu button or any unused touch point
*/

thread 'CARSPWN' 

// when cleo starts invokable script again it doesn't reset local variables
// so previous variable state can be saved
if or
    5@ < 400
    5@ > 611
then    
    5@ = 400
end  
   
// check GUI state 
0DDD: 0@ = get_mutex_var 0
077E: get_active_interior_to 1@
// and other player related stuff
if and
    0@ == 0
    1@ == 0
    Player.Defined($PLAYER_CHAR)
    Player.Controllable($PLAYER_CHAR)
jf @CARSPWN_END
if
    Actor.Driving($PLAYER_ACTOR)
then
    03E6: remove_text_box
    03E5: text_box 'CSPWN_4'
    jump @CARSPWN_END
end    
 
// set GUI indicator  
0DDC: set_mutex_var 0 to 1 

// freez player and start key loop
03E6: remove_text_box
03E5: text_box 'CSPWN_1' 
player.CanMove($PLAYER_CHAR) = false
0575: set_actor $PLAYER_ACTOR pinned_position 1
camera.Restore_WithJumpCut()
camera.SetBehindPlayer()

03F0: enable_text_draw 1
wait 500
jump @CARSPWN_95

:CARSPWN_83
wait 0 
5@ = 400 // integer values 

:CARSPWN_95
01E5: text_1number_highpriority 'NUMBER' 5@ flag 2000 time 1 
Model.Load(5@)
038B: load_requested_models 

:CARSPWN_120
wait 0
gosub @update_text 
if 
   Model.Available(5@)
else_jump @CARSPWN_120
07E4: get_model 5@ dimensions_cornerA_to 0@ 1@ 2@ dimensions_cornerB_to 10@ 11@ 12@
if 
    081F:  model 5@ plane
then    
    10@ *= 1.2
else
    10@ *= 2.2
    10@ += 1.0
end
04C4: create_coordinate 0@ 1@ 2@ from_actor $PLAYER_ACTOR offset 0.2 10@ 0.0 
3@ = Actor.Angle($PLAYER_ACTOR) 
if 
  3@ > 180.0 // floating-point values 
else_jump @CARSPWN_215 
3@ -= 180.0 // floating-point values 
jump @CARSPWN_225 

:CARSPWN_215
3@ += 180.0 // floating-point values 

:CARSPWN_225
4@ = Car.Create(5@, 0@, 1@, 2@)
3@ -= 90.0
Car.Angle(4@) = 3@
020A: set_car 4@ door_status_to 2 
Model.Destroy(5@)
0@ = 150
gosub @wait

:CARSPWN_262
wait 0
gosub @update_text 

0DE0: 0@ = get_touch_point_state 5 mintime 0
0DE0: 1@ = get_touch_point_state 4 mintime 0
0DE0: 2@ = get_touch_point_state 6 mintime 0 
if and
    0@ == 1
    1@ == 0
    2@ == 0
jf @CARSPWN_315

:END_TRUE 
020A: set_car 4@ door_status_to 0
car.RemoveReferences(4@) 
03D5: remove_text 'NUMBER'
03E6: remove_text_box 
03E5: text_box 'CSPWN_2'
player.CanMove($PLAYER_CHAR) = true
0575: set_actor $PLAYER_ACTOR pinned_position 0
0DDC: set_mutex_var 0 to 0
jump @CARSPWN_END 

:CARSPWN_315
if or
    player.Wasted($PLAYER_CHAR)
    0741: actor $PLAYER_ACTOR busted
then
    jump @END_FALSE
end

// check disable keys
0DE2: 0@ = get_menu_button_state // menu button
if
    0@ == 1
then
    jump @END_FALSE
end  
0DE0: 0@ = get_touch_point_state 1 mintime 500
0DE0: 1@ = get_touch_point_state 4 mintime 500
0DE0: 2@ = get_touch_point_state 7 mintime 500
if or
    0@ == 1 // touch point 1 is pressed
    1@ == 1 // touch point 4 is pressed
    2@ == 1 // touch point 7 is pressed
then
    jump @END_FALSE
end 
0DE0: 0@ = get_touch_point_state 3 mintime 250
if
    0@ == 1 // touch point 3 is pressed
then
    jump @END_FALSE
end  
jump @CARSPWN_364     

:END_FALSE
car.Destroy(4@)
03D5: remove_text 'NUMBER' 
03E6: remove_text_box
03E5: text_box 'CSPWN_3'
player.CanMove($PLAYER_CHAR) = true
0575: set_actor $PLAYER_ACTOR pinned_position 0
0DDC: set_mutex_var 0 to 0
jump @CARSPWN_END 

// check switch car right
:CARSPWN_364
0DE0: 0@ = get_touch_point_state 8 mintime 0
if 
    0@ == 1
else_jump @CARSPWN_474 
Car.Destroy(4@)
03D5: remove_text 'NUMBER' 
if 
   not 5@ == 611 // integer values 
else_jump @CARSPWN_83 
5@ += 1 // integer values 
if or
   5@ == 441
   5@ == 464
   5@ == 465
   5@ == 501
   5@ == 564
   5@ == 577
   5@ == 592
   5@ == 594
then
   jump @CARSPWN_603
end    
if or
   5@ == 449 
   5@ == 537
   5@ == 538
   5@ == 569
   5@ == 570
   5@ == 590 
then
   jump @CARSPWN_603
end       
jump @CARSPWN_95 

// check switch car left
:CARSPWN_474
0DE0: 0@ = get_touch_point_state 2 mintime 0
if 
    0@ == 1 
else_jump @CARSPWN_262 
Car.Destroy(4@)
03D5: remove_text 'NUMBER' 
if 
   not 5@ == 400 // integer values 
else_jump @CARSPWN_584 
5@ -= 1 // integer values 
if or
   5@ == 441
   5@ == 464
   5@ == 465
   5@ == 501
   5@ == 564
   5@ == 577
   5@ == 592   
   5@ == 594
then
   jump @CARSPWN_664
end    
if or
   5@ == 449 
   5@ == 537
   5@ == 538
   5@ == 569
   5@ == 570
   5@ == 590 
then
   jump @CARSPWN_664
end       
jump @CARSPWN_95 

:CARSPWN_584
5@ = 611 // integer values 
jump @CARSPWN_95 

:CARSPWN_603
if or
   5@ == 441
   5@ == 464
   5@ == 465
   5@ == 501
   5@ == 564
   5@ == 577
   5@ == 592   
   5@ == 594
then
   5@ += 1
   jump @CARSPWN_603
end    
if or
   5@ == 449 
   5@ == 537
   5@ == 538
   5@ == 569
   5@ == 570
   5@ == 590 
then
   5@ += 1
   jump @CARSPWN_603   
end 
jump @CARSPWN_95


:CARSPWN_664
if or
   5@ == 441
   5@ == 464
   5@ == 465
   5@ == 501
   5@ == 564
   5@ == 577
   5@ == 592   
   5@ == 594
then
   5@ -= 1
   jump @CARSPWN_664
end    
if or
   5@ == 449 
   5@ == 537
   5@ == 538
   5@ == 569
   5@ == 570
   5@ == 590 
then
   5@ -= 1
   jump @CARSPWN_664
end 
jump @CARSPWN_95

:CARSPWN_END
end_thread

// waits and draws the controls
:wait
while 0@ > 0 
  0@ -= 50
  wait 15
  gosub @update_text
end     
return

// updated text draw
:update_text
0342: set_text_draw_centered 1
033F: set_text_draw_letter_size 4.0 4.0
0340: set_text_draw_RGBA 255 0 0 255 
033E: set_draw_text_position 40.000 220.00 GXT 'CSPWN_L'

0342: set_text_draw_centered 1
033F: set_text_draw_letter_size 4.0 4.0
0340: set_text_draw_RGBA 255 0 0 255
033E: set_draw_text_position 600.00 220.00 GXT 'CSPWN_R'

0342: set_text_draw_centered 1
033F: set_text_draw_letter_size 3.0 3.0
0340: set_text_draw_RGBA 255 255 255 255 
033E: set_draw_text_position 40.000 360.00 GXT 'CSPWN_S' 
return
