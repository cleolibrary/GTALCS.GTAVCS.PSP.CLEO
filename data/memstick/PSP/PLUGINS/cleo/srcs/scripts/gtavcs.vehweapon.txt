{$CLEO .csa}

thread 'VEHWEAP'

var 
    0@ : Integer  // general purporse
    1@ : Integer  // player actor/char
    2@ : Integer  // player's car    
    3@ : Float    // coords from
    4@ : Float    // coords from
    5@ : Float    // coords from
    6@ : Float    // coords to
    7@ : Float    // coords to
    8@ : Float    // coords to
end
 
wait 1000

while true
    wait 0
    0DDD: 0@ = get_mutex_var 0
    if
        0@ == 1
    then
       wait 1000
       continue
    end
    0DD5: 0@ = get_platform
    if 
        0@ == 1
    then
        // 0004: 1@ = $537 // player handle on Android
    else
        0004: 1@ = $PLAYER_CHAR // player handle on PSP
    end     
    if or
        826E: not is_player_control_on 1@
        807E: not is_char_in_any_car 1@
    then
        continue
    end
    024B: store_car_char_is_in_no_save 1@ store_to 2@
    02BD: get_driver_of_car 2@ store_to 0@
    if or
        0@ <> 1@
        807F: not is_button_pressed 0 button 17 
        007F: is_button_pressed 0 button 4
        007F: is_button_pressed 0 button 6
    then
        continue
    end
    if or
        00A6: is_car_model 2@ model #HUNTER
        00A6: is_car_model 2@ model #SESPAROW
        00A6: is_car_model 2@ model #AUTOGYRO
        00A6: is_car_model 2@ model #RHINO
        00A6: is_car_model 2@ model #FIRETRUK
        00A6: is_car_model 2@ model #PREDATOR        
    then
        continue
    end
    if or
        00A6: is_car_model 2@ model #BIPLANE
        00A6: is_car_model 2@ model #REEFER
        00A6: is_car_model 2@ model #MARQUIS
        00A6: is_car_model 2@ model #RIO
        00A6: is_car_model 2@ model #TROPIC
    then
        continue
    end    
    027F: get_offset_from_car_in_world_coords 2@ offset 2.4 1.8 0.1 store_to 3@ 4@ 5@ 
    027F: get_offset_from_car_in_world_coords 2@ offset 2.4 10.0 0.1 store_to 6@ 7@ 8@
    04BC: create_projectile weapon 18 from 3@ 4@ 5@ to 6@ 7@ 8@ power 1.0 car_handle 2@ flag 0 store_to 0@
    027F: get_offset_from_car_in_world_coords 2@ offset -2.4 1.8 0.1 store_to 3@ 4@ 5@ 
    027F: get_offset_from_car_in_world_coords 2@ offset -2.4 10.0 0.1 store_to 6@ 7@ 8@
    04BC: create_projectile weapon 18 from 3@ 4@ 5@ to 6@ 7@ 8@ power 1.0 car_handle 2@ flag 0 store_to 0@
    wait 300 
end

