{$CLEO .csi}

thread 'CARSPWN'

var 
    0@ : Integer  // general purporse
    1@ : Integer  // general purporse
    2@ : Integer  // general purporse
    5@ : Float    // player/vehicle x
    6@ : Float    // player/vehicle y
    7@ : Float    // player/vehicle z
    8@ : Float    // player/vehicle heading
    10@ : Integer // selected item index
    11@ : Integer // active item index
    20@ : Integer // player handle
end  

0DD5: 0@ = get_platform
if 
    0@ == 1
then
    // 0004: 20@ = $537 // player handle on Android
else
    0004: 20@ = $PLAYER_CHAR // player handle on PSP
end

0DDD: 0@ = get_mutex_var 0
if or
    0@ == 1
    826E: not is_player_control_on 20@
then
    end_thread
end

0DDC: set_mutex_var 0 to 1    
    
0DD0: 0@ = get_label_addr @menu_desc
0DF2: create_menu 0@ items 110
0DF5: set_menu_active_item_index 11@
    
0107: set_player_control 20@ to 0
    
// touch handle loop
while true
    wait 0
    // resets menu touch item index as well
    0DF4: 10@ = get_menu_touched_item_index maxtime 100
    // spawn car
    if
        10@ >= 0
    then
        0@ = 10@
        0@ += 170
        if
            10@ >= 72
        then
           0@ += 1
        end
        0160: request_model 0@
        0228: load_all_models_now
        while 8161: not has_model_loaded 0@
            wait 0
        end
        02EE: get_offset_from_char_in_world_coords 20@ offset 0.2 4.8 0.0 store_to 5@ 6@ 7@
        00CE: get_char_heading 20@ store_to 8@
        if 
            8@ > 180.0 
        then 
            8@ -= 180.0 
        else
            8@ += 180.0
        end
        8@ -= 90.0 
        0048: create_car 0@ at 5@ 6@ 7@ store_to 1@
        00D1: set_car_heading 1@ to 8@
        013A: lock_car_doors 1@ mode 0
        0113: remove_references_to_car 1@
        0162: release_model 0@
        break
        //wait 2000
    end
    // close menu
    if 
        10@ == -2
    then
        break
    end
end

0DF6: 11@ = get_menu_active_item_index

// wait to show what was selected and not to trigger player controls with current touch
wait 100     
0DF3: delete_menu    
0107: set_player_control 20@ to 1
0DDC: set_mutex_var 0 to 0

end_thread     

:menu_desc
hex
    // flags
    00 00 00 00
    // title
    "CARSPAWNER" 00
    // close button
    "CLOSE" 00
    // items
    "6ATV" 00
    "ADMIRAL" 00
    "CHEETAH" 00
    "AUTOGYRO" 00
    "BAGGAGE" 00
    "BANSHEE" 00
    "PEREN" 00
    "BLISTAC" 00
    "BMXBOY" 00
    "BMXGIRL" 00
    "BOBCAT" 00
    "BULLDOZE" 00
    "BURRITO" 00
    "CABBIE" 00
    "CADDY" 00
    "SPEEDER2" 00
    "PIMP" 00
    "DELUXO" 00
    "HUEY" 00
    "HUEYHOSP" 00
    "ELECTRAG" 00
    "ELECTRAP" 00
    "ESPERANT" 00
    "FBICAR" 00
    "FIRETRUK" 00
    "GLENDALE" 00
    "GREENWOO" 00
    "HERMES" 00
    "HOVERCR" 00
    "IDAHO" 00
    "LANDSTAL" 00
    "MANANA" 00
    "MOP50" 00
    "OCEANIC" 00
    "VICECHEE" 00
    "SANCHEZ" 00
    "STALLION" 00
    "POLICEM" 00
    "BOBO" 00
    "PATRIOT" 00
    "PONY" 00
    "SENTINEL" 00
    "PCJ600" 00
    "MAVERICK" 00
    "REEFER" 00
    "SPEEDER" 00
    "LINERUN" 00
    "WALTON" 00
    "BARRACKS" 00
    "PREDATOR" 00
    "FLATBED" 00
    "AMMOTRUK" 00
    "BIPLANE" 00
    "MOONBEAM" 00
    "RUMPO" 00
    "YOLA" 00
    "TAXI" 00
    "AMBULAN" 00
    "STRETCH" 00
    "FAGGIO" 00
    "QUAD" 00
    "ANGEL" 00
    "FREEWAY" 00
    "JETSKI" 00
    "ENFORCE" 00
    "BOXVIL" 00
    "BENSON" 00
    "COACH" 00
    "MULE" 00
    "VOODOO" 00
    "SECURICA" 00
    "TRASH" 00
    "YANKEE" 00
    "MRWHOO" 00
    "SANDKING" 00
    "RHINO" 00
    "DINGHY" 00
    "MARQUIS" 00
    "RIO" 00
    "TROPIC" 00
    "FORKLIFT" 00
    "STREETFI" 00
    "VIRGO" 00
    "STINGER" 00
    "BFINJECT" 00
    "PHEONIX" 00
    "SQUALO" 00
    "JETMAX" 00
    "MESA" 00
    "VCNMAV" 00
    "POLMAV" 00
    "SPARROW" 00
    "SESPAROW" 00
    "SCARAB" 00
    "CHOLLO" 00
    "COMET" 00
    "CUBAN" 00
    "FBIRAN" 00
    "GANGBUR" 00
    "INFERNUS" 00
    "REGINA" 00
    "SABRE" 00
    "SABRETB" 00
    "SENTXS" 00
    "HUNTER" 00
    "WASHIN" 00
    "COASTG" 00
    "SKIMMER" 00
    "CHOPPER" 00
    00
end


